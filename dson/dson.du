import JSON;
import Object;
import System;


const annotationName = "Json";
const dictuTypes = ["number", "string", "set", "list", "dict"];

// Dson provides the ability to convert classes to JSON and 
// from JSON to a given class.
class Dson {
    init() {}

    // parse takes a string and a class instance, parses the string 
    // containing the JSON into a dictionary, and attempts to map
    // the fields to the given class with help from the class
    // annotations.
    parse(data, klass) {
        if (type(data) != "string") {
            return Error("data passed is not of type 'string'");
        }

        var res = Object.createFrom(klass._name);
        if (not res.success()) {
            return res;
        }
        res = res.unwrap();

        const parsed = JSON.parse(data);
        if (not parsed.success()) {
            return parsed;
        }

        const fields = klass.getAttributes()["fields"].filter(def(field) => field != "_name");
        const fieldAnnotations = klass._class.fieldAnnotations;

        fields.forEach(def(field) => {
            var fieldName = "";

            if (klass._class.fieldAnnotations != nil and fieldAnnotations.exists(field)) {
                if (fieldAnnotations.get(field).exists(annotationName)) {
                    fieldName = fieldAnnotations[field].get(annotationName);
                    res.setAttribute(field, parsed.unwrap()[fieldName]);
                }
            } else {
                fieldName = field;
                res.setAttribute(fieldName, parsed.unwrap()[fieldName]);
            }
        });

        return Success(res);
    }

    // processClass takes a class, parses the fields and annotations, parses the fields and 
    // data info a dictionary and returns the results. If a field is determined to be a class,
    // processClass will be called recursively on that type with the results being returned 
    // and added to the top level dict.
    private processClass(klass) {
        const fields = klass.getAttributes()["fields"].filter(def(field) => field != "_name");
        const fieldAnnotations = klass._class.fieldAnnotations;

        var data = {};

        fields.forEach(def(field) => {
            const fieldValue = klass.getAttribute(field);
            const fieldType = type(fieldValue);
            var fieldName = "";

            if (klass._class.fieldAnnotations != nil and fieldAnnotations.exists(field)) {
                if (fieldAnnotations.get(field).exists(annotationName)) {
                    fieldName = fieldAnnotations[field].get(annotationName);
                }
            } else {
                fieldName = field;
            }

            if (dictuTypes.contains(fieldType)) {
                data[fieldName] = fieldValue;
            } else {
                data[fieldName] = this.processClass(fieldValue);
            }
        });

        return data;
    }

    // stringify receives a class and returns a Result object
    // that unwraps to a JSON encoded string.
    stringify(klass) {
        const data = this.processClass(klass);
        return JSON.stringify(data);
    }
}

class Job {
    var title = "";
    var salary = 0;

    init() {}
}

class Person {
    @Json("first_name")
    var firstName = "";

    @Json("last_name")
    var lastName = "";

    var age = 0;
    var titles = [];
    var job = nil;

    init() {}
}

{ // main
    const dson = Dson();
    
    const job = Job();
    job.title = "Principal Software Engineer";
    job.salary = 10;

    const person = Person();
    person.firstName = "Brian";
    person.lastName = "Downs";
    person.age = 28;
    person.titles = ["Mr", "Dr", "MS"];
    person.job = job;

    // print(dson.stringify(person).unwrap());

    const payload1 = '{"title": "Engineer", "salary": 99}';
    const payload2 = '{"first_name": "Brian", "last_name": "Downs", "age": 28, "job": {"title": "Principal Software Engineer", "salary": 10}, "titles": ["Mr", "Dr", "MS"]}';
    const result1 = dson.parse(payload1, Job()).unwrap();
    const result2 = dson.parse(payload2, Person()).unwrap();
    print("Job.title: {}".format(result1.title));
    print("Job.salary: {}".format(result1.salary));
    print("First Name: {}".format(result2.firstName));
    print("Job: {}".format(result2.job));

    System.exit(0);
}
